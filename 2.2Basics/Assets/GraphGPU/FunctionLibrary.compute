//Parallel computation of the specific coordinates of each point

#pragma kernel FunctionKernel
#pragma kernel TransitionKernel
# define PI 3.14159265358979323846

//Corresponding to the positionBuffer in GPUGraph
//Binded by computeShader.SetBuffer(0, positionsId, positionBuffer);
RWStructuredBuffer<float3> _Positions;

uint _Resolution;

float _Step, _Time, _TransitionProgress;

uint _FunctionA, _FunctionB;

float2 GetUV(uint3 id){
	return (id.xy+0.5)*_Step-1.0;
}

void SetPosition(uint3 id, float3 position){
	if(id.x>=_Resolution||id.y>=_Resolution){
		return;
	}
	_Positions[id.x+id.y*_Resolution] = position;
}

float3 Wave(float u, float v, float t) {
    float3 p;
    p.x = u;
    p.y = sin(PI * (u + v + t));
    p.z = v;
    return p;
}

float3 MultiWave (float u, float v, float t) {
	float3 p;
	p.x = u;
	p.y = sin(PI * (u + 0.5 * t));
	p.y += 0.5 * sin(2.0 * PI * (v + t));
	p.y += sin(PI * (u + v + 0.25 * t));
	p.y *= 1.0 / 2.5;
	p.z = v;
	return p;
}

float3 Ripple (float u, float v, float t) {
	float d = sqrt(u * u + v * v);
	float3 p;
	p.x = u;
	p.y = sin(PI * (4.0 * d - t));
	p.y /= 1.0 + 10.0 * d;
	p.z = v;
	return p;
}

float3 Sphere (float u, float v, float t) {
	float r = 0.9 + 0.1 * sin(PI * (6.0 * u + 4.0 * v + t));
	float s = r * cos(0.5 * PI * v);
	float3 p;
	p.x = s * sin(PI * u);
	p.y = r * sin(0.5 * PI * v);
	p.z = s * cos(PI * u);
	return p;
}

float3 Torus (float u, float v, float t) {
	float r1 = 0.7 + 0.1 * sin(PI * (6.0 * u + 0.5 * t));
	float r2 = 0.15 + 0.05 * sin(PI * (8.0 * u + 4.0 * v + 2.0 * t));
	float s = r2 * cos(PI * v) + r1;
	float3 p;
	p.x = s * sin(PI * u);
	p.y = r2 * sin(PI * v);
	p.z = s * cos(PI * u);
	return p;
}

float3 GetPosition(uint index, float u, float v, float t){
	if(index==0){
		return Wave(u,v,t);
	}
	if(index==1){
		return MultiWave(u,v,t);
	}
	if(index==2){
		return Ripple(u,v,t);
	}
	if(index==3){
		return Sphere(u,v,t);
	}
	return Torus(u,v,t);
}

//define 8*8 thread group
//execute for each point
[numthreads(8,8,1)]
void FunctionKernel(uint3 id: SV_DISPATCHTHREADID){
	float2 uv = GetUV(id);
	SetPosition(id,GetPosition(_FunctionA,uv.x,uv.y,_Time));
}

[numthreads(8,8,1)]
void TransitionKernel(uint3 id: SV_DISPATCHTHREADID){
	float2 uv = GetUV(id);
	float3 posA = GetPosition(_FunctionA,uv.x,uv.y,_Time);
	float3 posB = GetPosition(_FunctionB,uv.x,uv.y,_Time);
	SetPosition(id, lerp(posA, posB, _TransitionProgress));
}